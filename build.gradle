import org.treesitter.build.GenTask

plugins {
    alias(libs.plugins.download)
    alias(libs.plugins.nexus.staging)
    id 'base'
}

allprojects {
    group = 'io.github.bonede'
    repositories {
        mavenCentral()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}


subprojects {
    apply plugin: 'java'
//    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation platform(libs.junit.bom)
        testImplementation 'org.junit.jupiter:junit-jupiter'
        if(project.name != 'tree-sitter'){
            testImplementation project(":tree-sitter-tests")
            implementation project(":tree-sitter")
        }
    }

    publishing {
        repositories {
            maven {
                name = "playerzero"
                def releasesRepoUrl = "https://nexus.playerzero.app/repository/maven-releases/"
                def snapshotsRepoUrl =  "https://nexus.playerzero.app/repository/maven-snapshots/"
                credentials {
                    username = System.getenv("NEXUS_USERNAME") ?: project.findProperty("pzNexusUsername").toString()
                    password = System.getenv("NEXUS_PASSWORD") ?: project.findProperty("pzNexusPassword").toString()
                }
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    url = 'https://github.com/goplayerzero/tree-sitter-ng'
                    description = "Next generation Tree Sitter Java binding"
                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/goplayerzero/tree-sitter-ng.git'
                        developerConnection = 'scm:git:https://github.com/goplayerzero/tree-sitter-ng.git'
                        url = 'https://github.com/goplayerzero/tree-sitter-ng'
                    }
                    developers {
                        developer {
                            id = 'marusic1514'
                            name = 'Maria Vinonokurskaya'
                            email = 'maria@playerzero.ai'
                        }
                        developer {
                            id = 'bonede'
                            name = 'Wang Liang'
                            email = 'bonede@qq.com'
                        }
                    }
                }
            }
        }
    }

//    signing {
//        sign configurations.archives
//        sign publishing.publications
//    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('downloadSource', DownloadSourceTask)

    tasks.register('buildNative', BuildNativeTask) {
        zigExe = rootProject.downloadZig.zigExe
        dependsOn downloadSource, rootProject.downloadZig
    }

    tasks.withType(Javadoc) {
        options.locale = 'en_US'
        options.JFlags = ["-Duser.language=en_US"]
    }
}


//nexusStaging {
//    serverUrl = "https://s01.oss.sonatype.org/service/local/"
//    packageGroup = "io.github.bonede"
//    username = ossrhUsername
//    password = ossrhPassword
//}

//tasks.register('downloadMiniSign', DownloadMiniSignTask)
//
//tasks.register('downloadZig', DownloadZigTask){
//    miniSignExe = downloadMiniSign.miniSignExe
//    dependsOn downloadMiniSign
//}

tasks.register("gen", GenTask) {
    group = "build setup"
    description = "Generate sub project for tree sitter parser"
}
